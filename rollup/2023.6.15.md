# Optimism L1 L2 通信机制

###概述

Optimism 上的应用程序可以通过称为“桥接”的过程与以太坊上的应用程序进行交互。简而言之，Optimism 上的合约可以触发以太坊上的合约功能，反之亦然。我们将从代码层面，也就是分析OP信使合约来剖析L1与L2之间是如何进行通信的。我看各种论坛上的对于OP的分析都是投资和炒币，对于技术方面的分析总是浅尝辄止，我就以源码的视角来分析L1与L2是如何进行通信的。

###CTC（Canonical Transaction Chain，规范事务链）
CTC是 Optimistic Rollup 解决方案的核心组件之一。它在 L1 上作为一个智能合约部署，负责处理和存储跨链交易。Optimistic Rollup 是一种以太坊扩展解决方案，旨在提高以太坊的可扩展性和吞吐量。

CTC 的功能如下：

存储交易队列：CTC 负责在 L1 上存储 L2 提交的交易。这些交易由 L2 的验证器节点按顺序提交，形成一个交易队列。
保持链上状态一致性：通过在 L1 上存储CTC，保持 L1 和 L2 之间的状态一致性。这对于跨链交互以及验证 L2 状态的正确性至关重要。
处理跨链交易：当 L1 需要与 L2 交互时，可以通过 CTC 提交交易。例如，将资产从 L1 转移到 L2 或者从 L2 转移到 L1。这些交易会被添加到交易队列中，并由 L2 验证器节点处理。
确保数据可用性：CTC 存储了跨链交易的数据，确保了数据可用性。这有助于防止验证器节点进行欺诈性行为，因为任何人都可以监控这些交易并验证其正确性。
提供验证和挑战机制：在 Optimistic Rollup 方案中，验证器节点需要提交验证交易，称为“Fraud Proof”。如果其他验证器节点认为某个提交的状态更新是错误的，他们可以对该状态更新发起挑战。CTC 负责处理这些挑战和验证交易，确保链上状态的正确性。

###SCC （State Commitment Chain，状态承诺链）
SCC是 Optimism的核心组件之一，用于存储状态根的链。SCC 的主要目的是保持链上所有状态根的记录，这些状态根是通过按顺序执行 Layer 2 上的事务计算得出的。SCC 中的状态根与 Canonical Transaction Chain（CTC）中的事务一一对应，以确保链上的状态连续且正确。

SCC 的主要功能包括：

存储状态批次：验证者可以通过调用 appendStateBatch() 函数将一批状态根追加到 SCC 中。
删除状态批次：在欺诈证明窗口内，OVM_FraudVerifier 可以通过调用 deleteStateBatch() 函数删除一个或多个状态批次。
验证状态承诺：通过调用 verifyStateCommitment() 函数，任何人都可以验证给定状态根是否包含在某个批次中。
检查批次头是否在欺诈证明窗口内：通过调用 insideFraudProofWindow() 函数，可以检查批次头是否在欺诈证明窗口内，从而确定是否可以对其进行欺诈证明。
SCC 是 Layer 1 和 Layer 2 之间通信的关键部分。它使得 Layer 1 可以持续监控 Layer 2 的状态变化，并在发现欺诈行为时执行欺诈证明，以保护链上的资产和用户。这种设计有效地实现了链下扩展性的提高，同时确保了链上安全性。

###From_L1_To_L2
介绍完两个OP中的重要组件之后，我们通过一个例子详细解读一下L1与L2之间是如何通信的。

根据官网上给出的例子，我们可以通过Goerli测试网络来进行我们的通信测试，L1与L2上部署了一个相同的合约Greeter.sol，下面分别为L1与L2中Greeter.sol的地址：

Goerli (L1)	0x7fA4D972bB15B71358da2D937E4A830A9084cf2e

Optimism Goerli (L2)	0xC0836cCc8FBa87637e782Dde6e6572aD624fb984

1. 通过Hardhat连接到Optimism Goerli (L2)网络中。
2. 在Goerli (L1)中部署FromL1_ControlL2Greeter合约.合约与部署代码如下： 其中，sendMessage的第一个参数为L2接受消息的目的合约地址，第二个参数为发送的消息，需要用abi.encodeWithSignature将函数名与其参数编码，第三个参数为Gas Limit。

		//SPDX-License-Identifier: Unlicense
		
		Greeter = await ethers.getContractFactory("Greeter")
		greeter = await Greeter.attach("0xC0836cCc8FBa87637e782Dde6e6572aD624fb984")
		await greeter.greet()
		// For cross domain messages' origin
		import { ICrossDomainMessenger } from 
		 "@eth-optimism/contracts/libraries/bridge/ICrossDomainMessenger.sol";
		​
		contract Greeter {
		 string greeting;
		​
		 event SetGreeting(
		 address sender,     // msg.sender
		 address origin,     // tx.origin
		 address xorigin,    // cross domain origin, if any
		 address user,       // user address, if given
		 string greeting // The greeting 
		    );
		 
		​
		 constructor(string memory _greeting) {
		 greeting = _greeting;
		  }
		​
		 function greet() public view returns (string memory) {
		 return greeting;
		  }
		​
		 function setGreeting(string memory _greeting, address _user) public {
		 greeting = _greeting;
		 emit SetGreeting(msg.sender, tx.origin, getXorig(), _user, _greeting);
		  }
		​
		​
		 function setGreeting(string memory _greeting) public {
		 greeting = _greeting;
		 emit SetGreeting(msg.sender, tx.origin, getXorig(), address(0), _greeting);
		  }
		​
		 // Get the cross domain origin, if any
		 function getXorig() private view returns (address) {
		 // Get the cross domain messenger's address each time.
		 // This is less resource intensive than writing to storage.
		 address cdmAddr = address(0);    
		​
		 // Mainnet
		 if (block.chainid == 1)
		 cdmAddr = 0x25ace71c97B33Cc4729CF772ae268934F7ab5fA1;
		​
		 // Goerli
		 if (block.chainid == 5)
		 cdmAddr = 0x5086d1eEF304eb5284A0f6720f79403b4e9bE294;
		​
		 // L2 (same address on every network)
		 if (block.chainid == 10 || block.chainid == 420)
		 cdmAddr = 0x4200000000000000000000000000000000000007;
		​
		 // If this isn't a cross domain message
		 if (msg.sender != cdmAddr)
		 return address(0);
		​
		 // If it is a cross domain message, find out where it is from
		 return ICrossDomainMessenger(cdmAddr).xDomainMessageSender();
		  }    // getXorig()
		}   // contract Greeter
通过Hardhat连接到Goerli (L1)网络。

		// This contracts runs on L1, and controls a Greeter on L2.
		// The addresses are specific to Optimistic Goerli.
		pragma solidity ^0.8.0;
		​
		import { ICrossDomainMessenger } from 
		 "@eth-optimism/contracts/libraries/bridge/ICrossDomainMessenger.sol";
		 
		contract FromL1_ControlL2Greeter {
		 // Taken from https://community.optimism.io/docs/useful-tools/networks/#optimism-goerli
		​
		 address crossDomainMessengerAddr = 0x5086d1eEF304eb5284A0f6720f79403b4e9bE294;
		​
		 address greeterL2Addr = 0xE8B462EEF7Cbd4C855Ea4B65De65a5c5Bab650A9;
		​
		 function setGreeting(string calldata _greeting) public {
		 bytes memory message;
		 
		 message = abi.encodeWithSignature("setGreeting(string,address)", 
		 _greeting, msg.sender);
		​
		 ICrossDomainMessenger(crossDomainMessengerAddr).sendMessage(
		 greeterL2Addr,
		 message,
		 1000000 // within the free gas limit amount
		        );
		    }      // function setGreeting 
		​
		}          // contract FromL1_ControlL2Greeter
		​
		Controller = await ethers.getContractFactory("FromL1_ControlL2Greeter")
		controller = await Controller.deploy()
		tx = await controller.setGreeting(`Hello from L1 ${Date()}`)
		rcpt = await tx.wait()

部署之后五分钟左右就可以在L2出看见Greet的内容。
这里我们可以看到，部署的FromL1_ControlL2Greeter合约来进行对L2消息的传递，其中的SendMessage函数是留给我们传递信息的接口，我们接下来就通过源码，逐步分析。

	ICrossDomainMessenger(crossDomainMessengerAddr).sendMessage
通过阅读源码可以发现，在L1与L2之间都部署了信使合约，分别为L1CrossDomainMessenger.sol与L2CrossDomainMessenger.sol，上述调用的SendMessage函数就是这两个合约对IL1CrossDomainMessenger.sol接口的具体实现。

L1在调用SendMessage函数之后，程序将进入下面代码：

	function sendMessage(
        address _target,
        bytes memory _message,
        uint32 _gasLimit
    ) public {
        address ovmCanonicalTransactionChain = resolve("CanonicalTransactionChain");
        // Use the CTC queue length as nonce
        uint40 nonce = ICanonicalTransactionChain(ovmCanonicalTransactionChain).getQueueLength();​
        bytes memory xDomainCalldata = Lib_CrossDomainUtils.encodeXDomainCalldata(
            _target,
            msg.sender,
            _message,
            nonce
        );
​
        // slither-disable-next-line reentrancy-events
        _sendXDomainMessage(ovmCanonicalTransactionChain, xDomainCalldata, _gasLimit);​
        // slither-disable-next-line reentrancy-events
        emit SentMessage(_target, msg.sender, _message, nonce, _gasLimit);
    }

这段代码首先将L1的CTC链的地址通过别名解析出来，然后将nouce设置为CTC链当前队列的长度。
将所发信息编码为xDomainCalldata，并调用_sendXDomainMessage函数。
触发SentMessage事件，用于链外事件的接收和响应，以及日志记录。
xDomainCalldata的格式如下，用于调用relayMessage方法。

	function encodeXDomainCalldata(
        address _target,
        address _sender,
        bytes memory _message,
        uint256 _messageNonce
    ) internal pure returns (bytes memory) {
        return
            abi.encodeWithSignature(
                "relayMessage(address,address,bytes,uint256)",
                _target,
                _sender,
                _message,
                _messageNonce
            );
    }
	_sendXDomainMessage函数如下：

 	function _sendXDomainMessage(
        address _canonicalTransactionChain,
        bytes memory _message,
        uint256 _gasLimit
    ) internal {
        // slither-disable-next-line reentrancy-events
        ICanonicalTransactionChain(_canonicalTransactionChain).enqueue(
            Lib_PredeployAddresses.L2_CROSS_DOMAIN_MESSENGER,
            _gasLimit,
            _message
        );
    }
_sendXDomainMessage函数将CTC地址传入，实例化ICanonicalTransactionChain合约，然后调用CanonicalTransactionChain.sol中的enqueue方法，将被编码的消息入队。

之后我们分析CanonicalTransactionChain.sol的enqueue方法，看看数据会如何操作，enqueue方法如下：

	function enqueue(
        address _target,
        uint256 _gasLimit,
        bytes memory _data
    ) external {
        require(
            _data.length <= MAX_ROLLUP_TX_SIZE,
            "Transaction data size exceeds maximum for rollup transaction."
        );

        require(
            _gasLimit <= maxTransactionGasLimit,
            "Transaction gas limit exceeds maximum for rollup transaction."
        );

        require(_gasLimit >= MIN_ROLLUP_TX_GAS, "Transaction gas limit too low to enqueue.");

        if (_gasLimit > enqueueL2GasPrepaid) {
            uint256 gasToConsume = (_gasLimit - enqueueL2GasPrepaid) / l2GasDiscountDivisor;
            uint256 startingGas = gasleft();

            // Although this check is not necessary (burn below will run out of gas if not true), it
            // gives the user an explicit reason as to why the enqueue attempt failed.
            require(startingGas > gasToConsume, "Insufficient gas for L2 rate limiting burn.");

            uint256 i;
            while (startingGas - gasleft() < gasToConsume) {
                i++;
            }
        }
        address sender;
        if (msg.sender == tx.origin) {
            sender = msg.sender;
        } else {
            sender = AddressAliasHelper.applyL1ToL2Alias(msg.sender);
        }

        bytes32 transactionHash = keccak256(abi.encode(sender, _target, _gasLimit, _data));

        queueElements.push(
            Lib_OVMCodec.QueueElement(
                transactionHash: transactionHash,
                timestamp: uint40(block.timestamp),
                blockNumber: uint40(block.number)
            })
        );
        uint256 queueIndex = queueElements.length - 1;
        emit TransactionEnqueued(sender, _target, _gasLimit, _data, queueIndex, block.timestamp);
    }
前面的大部分代码都是进行Gas计算和身份认证，对于数据的操作就是将数据编码过后进行Hash，然后将其Push到CTC的队列中，最关键的操作为：

emit TransactionEnqueued(sender, _target, _gasLimit, _data, queueIndex, block.timestamp);
这个事件其实就是L1与L2间传递消息的通道，L1将CTC作为桥梁，来进行消息的传递。CTC中触发了这个消息入队事件，事件中包括我们要传输的数据_data。

因为CTC合约在L1上是预编译的，因此合约地址是固定的，所以L2客户端可以根据CTC合约的地址来进行对合约上事件的监听，OP中通过一直运行一个监听服务实现的，具体实现细节如下，代码地址xxxxxxx

	protected async _start(): Promise<void> {
    
    while (this.running) {
      try {
        const highestSyncedL1Block =
          (await this.state.db.getHighestSyncedL1Block()) ||
          this.state.startingL1BlockNumber
        const currentL1Block = await this.state.l1RpcProvider.getBlockNumber()
        let targetL1Block = Math.min(
          highestSyncedL1Block + this.options.logsPerPollingInterval,
          currentL1Block - this.options.confirmations
        )

        // Don't sync beyond the shutoff block!
        if (Number.isInteger(this.options.l1SyncShutoffBlock)) {
          targetL1Block = Math.min(
            targetL1Block,
            this.options.l1SyncShutoffBlock
          )
        }

        // We're already at the head, so no point in attempting to sync.
        if (highestSyncedL1Block === targetL1Block) {
          await sleep(this.options.pollingInterval)
          continue
        }

        this.logger.info('Synchronizing events from Layer 1 (Ethereum)', {
          highestSyncedL1Block,
          targetL1Block,
        })

        const depositShutoffBlock = BigNumber.from(
          await this.state.contracts.Lib_AddressManager.getAddress(
            'DTL_SHUTOFF_BLOCK'
          )
        ).toNumber()

        // If the deposit shutoff block is set, then we should stop syncing deposits at that block.
        let depositTargetL1Block = targetL1Block
        if (depositShutoffBlock > 0) {
          this.logger.info(`Deposit shutoff active`, {
            targetL1Block,
            depositShutoffBlock,
          })
          depositTargetL1Block = Math.min(
            depositTargetL1Block,
            depositShutoffBlock
          )
        }

        // We should not sync TransactionEnqueued events beyond the deposit shutoff block.
        if (depositTargetL1Block >= highestSyncedL1Block) {
          await this._syncEvents(
            'CanonicalTransactionChain',
            'TransactionEnqueued',
            highestSyncedL1Block,
            depositTargetL1Block,
            handleEventsTransactionEnqueued
          )
        } else {
          this.logger.info('Deposit shutoff reached', {
            depositTargetL1Block,
            highestSyncedL1Block,
            depositShutoffBlock,
          })
        }

        await this._syncEvents(
          'CanonicalTransactionChain',
          'SequencerBatchAppended',
          highestSyncedL1Block,
          targetL1Block,
          handleEventsSequencerBatchAppended
        )

        await this._syncEvents(
          'StateCommitmentChain',
          'StateBatchAppended',
          highestSyncedL1Block,
          targetL1Block,
          handleEventsStateBatchAppended
        )

        await this.state.db.setHighestSyncedL1Block(targetL1Block)

        this.l1IngestionMetrics.highestSyncedL1Block.set(targetL1Block)

        if (
          currentL1Block - highestSyncedL1Block <
          this.options.logsPerPollingInterval
        ) {
          await sleep(this.options.pollingInterval)
        }
      } catch (err) {
        if (err instanceof MissingElementError) {
          this.logger.warn('recovering from a missing event', {
            message: err.toString(),
          })

          const handlers = {
            SequencerBatchAppended:
              this.state.db.getLatestTransactionBatch.bind(this.state.db),
            SequencerBatchAppendedTransaction:
              this.state.db.getLatestTransaction.bind(this.state.db),
            StateBatchAppended: this.state.db.getLatestStateRootBatch.bind(
              this.state.db
            ),
            TransactionEnqueued: this.state.db.getLatestEnqueue.bind(
              this.state.db
            ),
          }

          const eventName = err.name
          if (!(eventName in handlers)) {
            throw new Error(
              `unable to recover from missing event, no handler for ${eventName}`
            )
          }

          const lastGoodElement: {
            blockNumber: number
          } = await handlers[eventName]()

          if (lastGoodElement === null) {
            throw new Error(`unable to recover from missing event`)
          }
            
          await this.state.db.setHighestSyncedL1Block(
            lastGoodElement.blockNumber
          )
            
          this.l1IngestionMetrics.highestSyncedL1Block.set(
            lastGoodElement.blockNumber
          )

          this.logger.warn('recovered from a missing event', {
            eventName,
            lastGoodBlockNumber: lastGoodElement.blockNumber,
          })

          this.l1IngestionMetrics.missingElementCount.inc()
        } else if (!this.running || this.options.dangerouslyCatchAllErrors) {
          this.l1IngestionMetrics.unhandledErrorCount.inc()
          this.logger.error('Caught an unhandled error', {
            message: err.toString(),
            stack: err.stack,
            code: err.code,
          })
          await sleep(this.options.pollingInterval)
        } else {
          throw err
        }
      }
    }
  }
这里通过_syncEvent在L2链上进行对L1链事件的同步，并且事件解析并存入Transport DB中。

	await this._syncEvents(
            'CanonicalTransactionChain',
            'TransactionEnqueued',
            highestSyncedL1Block,
            depositTargetL1Block,
            handleEventsTransactionEnqueued
          )
但是这里只是L2客户端得到了数据，即在链下获得了数据，还需要转移到链上，这时通过前面编码的xDomainCalldata直接调用L2CrossDomainMessenger.sol中的relayMessage方法将其从链下转发到链上。

	function relayMessage(
        address _target,
        address _sender,
        bytes memory _message,
        uint256 _messageNonce
    ) public {
        // Since it is impossible to deploy a contract to an address on L2 which matches
        // the alias of the L1CrossDomainMessenger, this check can only pass when it is called in
        // the first call from of a deposit transaction. Thus reentrancy is prevented here.
        require(
            AddressAliasHelper.undoL1ToL2Alias(msg.sender) == l1CrossDomainMessenger,
            "Provided message could not be verified."
        );

        bytes memory xDomainCalldata = Lib_CrossDomainUtils.encodeXDomainCalldata(
            _target,
            _sender,
            _message,
            _messageNonce
        );

        bytes32 xDomainCalldataHash = keccak256(xDomainCalldata);

        require(
            successfulMessages[xDomainCalldataHash] == false,
            "Provided message has already been received."
        );

        // Prevent calls to OVM_L2ToL1MessagePasser, which would enable
        // an attacker to maliciously craft the _message to spoof
        // a call from any L2 account.
        if (_target == Lib_PredeployAddresses.L2_TO_L1_MESSAGE_PASSER) {
            // Write to the successfulMessages mapping and return immediately.
            successfulMessages[xDomainCalldataHash] = true;
            return;
        }

        xDomainMsgSender = _sender;
        // slither-disable-next-line reentrancy-no-eth, reentrancy-events, reentrancy-benign
        (bool success, ) = _target.call(_message);
        // slither-disable-next-line reentrancy-benign
        xDomainMsgSender = Lib_DefaultValues.DEFAULT_XDOMAIN_SENDER;

        // Mark the message as received if the call was successful. Ensures that a message can be
        // relayed multiple times in the case that the call reverted.
        if (success == true) {
            // slither-disable-next-line reentrancy-no-eth
            successfulMessages[xDomainCalldataHash] = true;
            // slither-disable-next-line reentrancy-events
            emit RelayedMessage(xDomainCalldataHash);
        } else {
            // slither-disable-next-line reentrancy-events
            emit FailedRelayedMessage(xDomainCalldataHash);
        }

        // Store an identifier that can be used to prove that the given message was relayed by some
        // user. Gives us an easy way to pay relayers for their work.
        bytes32 relayId = keccak256(abi.encodePacked(xDomainCalldata, msg.sender, block.number));

        // slither-disable-next-line reentrancy-benign
        relayedMessages[relayId] = true;
    }
relayMessage中前面的一些操作是验证数据的完整性以及发送者的正确性。其关键在于

	(bool success, ) = _target.call(_message);
这一行代码才是整个转发过程的重点所在，_target为目的合约的地址，_message为编码后的数据，包含目的合约的方法名和参数。因此，如果目的合约地址正确且信息正确，则信息传递成功并返回success。然后根据返回的情况来进行一些防止攻击的操作。

让我们回顾一下L1到L2消息传递都经历了那些步骤：

L1通过FromL1_ControlL2Greeter.sol的SendMessage函数向L2上部署的
Greeter.sol智能合约发送消息。其中参数为：Greeter.sol在L2上的地址、触发Greeter.sol合约函数setGreeting的编码信息以及Gas Limit
消息转到L1上CTC合约CanonicalTransactionChain.sol，CTC合约将消息编码后入队，触发TransactionEnqueued事件。
因为L1上CanonicalTransactionChain.sol是预编译的，地址是固定的。L2客户端会通过启动服务程序来监听此事件，一旦发现事件被触发，则调用_sycnEvnet对事件进行解析。
此时数据仍在链下，L2客户端调用L2CrossDomainMessenger.sol中的relayMessage函数将链下的数据转发到链上进行操作。
在relayMessage中，_target为L2智能合约Greeter.sol的地址。_message为FromL1_ControlL2Greeter.sol中编码的数据。通过对目标智能合约的调用来进行数据的传递：(bool success, ) = _target.call(_message);
这样在L2部署的Greeter.sol中，SetGreeting方法被调用，完成消息的传递。